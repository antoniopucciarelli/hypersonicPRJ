# seed generation
seed 12345

# 3D simulation
dimension 3

# global settings declaration
global surfmax 100000
global splitmax 15
global gridcut 0.1 comm/sort yes 

# boundary conditions
boundary oo oo oo

# Xmin, Xmax  Ymin, Ymax  Zmin, Zmax
create_box -10 10 -10 10 -10 10

# Number of cells, only 1 along z
create_grid 200 100 100

# setting parallel run 
balance_grid rcb cell

# number density 
global nrho 5.84E15 fnum 1E15

# define a gas species 
species air.species N2

# free-stream velocity and temperature
mixture nitrogen N2 vstream 8000.0 0.0 0.0 temp 850 

# no gas-phase collisions
collide none

# import geometry
read_surf data.orionSimple origin 0.0 0.0 0.0 rotate 180 0 0 1

# create a gas-surface collision type and give it the ID = 1
surf_collide 1 specular

# assign collisions to surfaces
surf_modify all collide 1

# inject gas from Xmin surface
fix fixInject emit/face nitrogen xlo ylo yhi

# timestep (not critical in this free molecular simulation)
timestep 5.0E-7 # [s]

# print stats each N timesteps 
stats 100

# step     = timestep
# cpu      = elapsed CPU time in seconds within a run
# np       = number of particles (this step)
# nscoll   = number of surface collisions (this step)
# ncoll    = number of particle/particle collisions (this step)
# nattempt = number of attempted collisions (this step)
stats_style step cpu np nattempt ncoll

# run simulation
run 1500

# grid adaptation
fix adaptgrid adapt 500 all refine particle 50 1

# run simulation after grid refinement
run 1500

# unfixing adapt grid 
unfix adaptgrid

# computing forces 
compute fx surf all all fx 
compute fz surf all all fz 

# computing average forces 
fix fxAVE ave/surf all 1 10 100 c_fx[*] ave one 
fix fzAVE ave/surf all 1 10 100 c_fz[*] ave one

# computing total forces 
compute fxTOT reduce sum f_fxAVE[*]
compute fzTOT reduce sum f_fzAVE[*]

# saving data
compute n         grid          all nitrogen n
compute nrho      grid          all nitrogen nrho
compute velocity  grid          all nitrogen u v w
compute temp_tra  thermal/grid  all nitrogen temp

fix avg_n         ave/grid all  1 10 100 c_n[*]        ave running
fix avg_nrho      ave/grid all  1 10 100 c_nrho[*]     ave running
fix avg_velocity  ave/grid all  1 10 100 c_velocity[*] ave running
fix avg_temp      ave/grid all  1 10 100 c_temp_tra[*] ave running

# saving grid 
shell mkdir grid
write_grid ./grid/gridOrionSimple

# saving computed results
shell mkdir dump
dump  myDumpID  grid  all  100  ./dump/flow_field.* id proc &
      f_avg_n   f_avg_nrho  f_avg_temp f_avg_velocity[*]

# printing style 
stats_style step cpu np nattempt ncoll c_fxTOT c_fzTOT

# run simulation 
run 2000	
